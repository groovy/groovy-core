def subprojects = ['groovy-ant',
        'groovy-bsf',
        'groovy-console',
        'groovy-docgenerator',
        'groovy-groovydoc',
        'groovy-groovysh',
        'groovy-jmx',
        'groovy-json',
        'groovy-jsr223',
        'groovy-servlet',
        'groovy-sql',
        'groovy-swing',
        'groovy-templates',
        'groovy-test',
        'groovy-testng',
        'groovy-xml']

if(JavaVersion.current().isJava7Compatible()) {
    subprojects << 'groovy-nio'
}

if(JavaVersion.current().isJava8Compatible()) {
    subprojects << 'groovy-optional'
}
 
include(subprojects as String[])
        
rootProject.children.each { prj ->
    prj.projectDir = new File("$rootDir/subprojects/$prj.name")
}

rootProject.name = 'groovy' // TODO should this be groovy-core?
